# sdl3_mixer cmake project-config input for CMakeLists.txt script

@PACKAGE_INIT@

include(FeatureSummary)
set_package_properties(SDL3_mixer PROPERTIES
    URL "https://www.libsdl.org/projects/SDL_mixer/"
    DESCRIPTION "SDL_mixer is a sample multi-channel audio mixer library"
)

set(SDL3_mixer_FOUND                ON)

set(SDL3MIXER_VENDORED              @SDL3MIXER_VENDORED@)

set(SDL3MIXER_CMD                   @SDL3MIXER_CMD_ENABLED@)

set(SDL3MIXER_SNDFILE               @SDL3MIXER_SNDFILE_ENABLED@)

set(SDL3MIXER_FLAC                  @SDL3MIXER_FLAC_ENABLED@)
set(SDL3MIXER_FLAC_LIBFLAC          @SDL3MIXER_FLAC_LIBFLAC_ENABLED@)
set(SDL3MIXER_FLAC_DRFLAC           @SDL3MIXER_FLAC_DRFLAC_ENABLED@)

set(SDL3MIXER_GME                   @SDL3MIXER_GME_ENABLED@)

set(SDL3MIXER_MOD                   @SDL3MIXER_MOD_ENABLED@)
set(SDL3MIXER_MOD_MODPLUG           @SDL3MIXER_MOD_MODPLUG_ENABLED@)
set(SDL3MIXER_MOD_XMP               @SDL3MIXER_MOD_XMP_ENABLED@)
set(SDL3MIXER_MOD_XMP_LITE          @SDL3MIXER_MOD_XMP_ENABLED@)

set(SDL3MIXER_MP3                   @SDL3MIXER_MP3_ENABLED@)
set(SDL3MIXER_MP3_DRMP3             @SDL3MIXER_MP3_DRMP3_ENABLED@)
set(SDL3MIXER_MP3_MPG123            @SDL3MIXER_MP3_MPG123_ENABLED@)

set(SDL3MIXER_MIDI                  @SDL3MIXER_MIDI_ENABLED@)
set(SDL3MIXER_MIDI_FLUIDSYNTH       @SDL3MIXER_MIDI_FLUIDSYNTH_ENABLED@)
set(SDL3MIXER_MIDI_NATIVE           @SDL3MIXER_MIDI_NATIVE_ENABLED@)
set(SDL3MIXER_MIDI_TIMIDITY         @SDL3MIXER_MIDI_TIMIDITY_ENABLED@)

set(SDL3MIXER_OPUS                  @SDL3MIXER_OPUS_ENABLED@)

set(SDL3MIXER_VORBIS                @SDL3MIXER_VORBIS_ENABLED@)
set(SDL3MIXER_VORBIS_STB            @SDL3MIXER_VORBIS_STB_ENABLED@)
set(SDL3MIXER_VORBIS_TREMOR         @SDL3MIXER_VORBIS_TREMOR_ENABLED@)
set(SDL3MIXER_VORBIS_VORBISFILE     @SDL3MIXER_VORBIS_VORBISFILE_ENABLED@)

set(SDL3MIXER_WAVE                  @SDL3MIXER_WAVE_ENABLED@)

set(SDL3MIXER_WAVPACK               @SDL3MIXER_WAVPACK_ENABLED@)

set(SDL3MIXER_SDL3_REQUIRED_VERSION @SDL_REQUIRED_VERSION@)

if(EXISTS "${CMAKE_CURRENT_LIST_DIR}/SDL3_mixer-shared-targets.cmake")
    include("${CMAKE_CURRENT_LIST_DIR}/SDL3_mixer-shared-targets.cmake")
endif()

if(EXISTS "${CMAKE_CURRENT_LIST_DIR}/SDL3_mixer-static-targets.cmake")

    if(SDL3MIXER_VENDORED)
        if(SDL3MIXER_GME)
            include(CheckLanguage)
            check_language(CXX)
            if(NOT CMAKE_CXX_COMPILER)
                message(WARNING "CXX language not enabled. Linking to SDL3_mixer::SDL3_mixer-static might fail.")
            endif()
        endif()
    else()
        include(CMakeFindDependencyMacro)
        include(PkgConfigHelper)

        set(_sdl_cmake_module_path "${CMAKE_MODULE_PATH}")
        list(APPEND CMAKE_MODULE_PATH "${CMAKE_CURRENT_LIST_DIR}")

        if(NOT DEFINED CMAKE_FIND_PACKAGE_PREFER_CONFIG)
            set(CMAKE_FIND_PACKAGE_PREFER_CONFIG ON)
        endif()

        if(SDL3MIXER_SNDFILE AND NOT TARGET SndFile::sndfile)
            find_dependency(SndFile)
        endif()

        if(SDL3MIXER_FLAC_LIBFLAC AND NOT TARGET FLAC::FLAC)
            find_dependency(FLAC)
        endif()

        if(SDL3MIXER_MOD_MODPLUG AND NOT TARGET modplug::modplug)
            find_dependency(modplug)
        endif()

        if(SDL3MIXER_MOD_XMP AND NOT TARGET libxmp::libxmp)
            find_dependency(libxmp)
        endif()

        if(SDL3MIXER_MOD_XMP_LITE AND NOT TARGET libxmp-lite::libxmp-lite)
            find_dependency(libxmp-lite)
        endif()

        if(SDL3MIXER_MP3_MPG123 AND NOT TARGET MPG123::mpg123)
            find_dependency(mpg123)
        endif()

        if(SDL3MIXER_MIDI_FLUIDSYNTH AND NOT TARGET FluidSynth::libfluidsynth)
            find_dependency(FluidSynth)
        endif()

        if(SDL3MIXER_VORBIS_TREMOR AND NOT TARGET tremor::tremor)
            find_dependency(tremor)
        endif()

        if(SDL3MIXER_VORBIS_VORBISFILE AND NOT TARGET Vorbis::vorbisfile)
            find_dependency(Vorbis)
        endif()

        if(SDL3MIXER_OPUS AND NOT TARGET OpusFile::opusfile)
            find_dependency(OpusFile)
        endif()

        if(SDL3MIXER_WAVPACK AND NOT TARGET WavPack::WavPack)
            find_dependency(wavpack)
        endif()

        set(CMAKE_MODULE_PATH "${_sdl_cmake_module_path}")
        unset(_sdl_cmake_module_path)

        if(HAIKU AND SDL3MIXER_MIDI_NATIVE)
            include(CheckLanguage)
            check_language(CXX)
            if(NOT CMAKE_CXX_COMPILER)
                message(WARNING "CXX language not enabled. Linking to SDL3_mixer::SDL3_mixer-static might fail.")
            endif()
        endif()
    endif()

    include("${CMAKE_CURRENT_LIST_DIR}/SDL3_mixer-static-targets.cmake")
endif()

function(_sdl_create_target_alias_compat NEW_TARGET TARGET)
    if(CMAKE_VERSION VERSION_LESS "3.18")
        # Aliasing local targets is not supported on CMake < 3.18, so make it global.
        add_library(${NEW_TARGET} INTERFACE IMPORTED)
        set_target_properties(${NEW_TARGET} PROPERTIES INTERFACE_LINK_LIBRARIES "${TARGET}")
    else()
        add_library(${NEW_TARGET} ALIAS ${TARGET})
    endif()
endfunction()

# Make sure SDL3_mixer::SDL3_mixer always exists
if(NOT TARGET SDL3_mixer::SDL3_mixer)
    if(TARGET SDL3_mixer::SDL3_mixer-shared)
        _sdl_create_target_alias_compat(SDL3_mixer::SDL3_mixer SDL3_mixer::SDL3_mixer-shared)
    else()
        _sdl_create_target_alias_compat(SDL3_mixer::SDL3_mixer SDL3_mixer::SDL3_mixer-static)
    endif()
endif()
